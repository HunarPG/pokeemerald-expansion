const Rival = 8

mapscripts Jubilife_City_School_MapScripts {}

script Jubilife_City_School_EventScripts_Barry
{
    lock()
    faceplayer()
    msgbox(Jubilife_City_School_Text_Barry_What_Brings_You_Here, MSGBOX_SIGN)
    playfanfare(MUS_DP_OBTAIN_ITEM)
    removeitem(ITEM_PARCEL)
    msgbox(Jubilife_City_School_Text_Barry_Player_Gives_Parcel, MSGBOX_SIGN)
    msgbox(Jubilife_City_School_Text_Barry_Gives_You_Town_Map, MSGBOX_SIGN)
    giveitem(ITEM_TOWN_MAP)
    msgbox(Jubilife_City_School_Text_Barry_Where_To_Go_Next, MSGBOX_SIGN)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    goto_if_eq(VAR_RESULT, DIR_EAST, Jubilife_City_School_EventScripts_Barry_Leaves_Player_Face_Right)
    goto_if_eq(VAR_RESULT, DIR_WEST, Jubilife_City_School_EventScripts_Barry_Leaves_Player_Face_Left)
    goto_if_eq(VAR_RESULT, DIR_NORTH, Jubilife_City_School_EventScripts_Barry_Leaves_Player_Face_Up)
}

script Jubilife_City_School_EventScripts_Barry_Leaves_Player_Face_Right
{
    applymovement(Rival, Movement_Rival_Goes_Halfway_Down_In_School_Player_Face_Right_Or_Left)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Face_Down)
    applymovement(Rival, Movement_Rival_Fully_Leaves_Player_Face_Right_Or_Left)
    waitmovement(0)
    playse(SE_EXIT)
    removeobject(Rival)
    setflag(FLAG_HIDE_BARRY_IN_SCHOOL)
    release()
    end()
}

script Jubilife_City_School_EventScripts_Barry_Leaves_Player_Face_Left
{
    applymovement(Rival, Movement_Rival_Goes_Halfway_Down_In_School_Player_Face_Right_Or_Left)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Face_Down)
    applymovement(Rival, Movement_Rival_Fully_Leaves_Player_Face_Right_Or_Left)
    waitmovement(0)
    playse(SE_EXIT)
    removeobject(Rival)
    setflag(FLAG_HIDE_BARRY_IN_SCHOOL)
    release()
    end()
}

script Jubilife_City_School_EventScripts_Barry_Leaves_Player_Face_Up
{
    applymovement(Rival, Movement_Rival_Walks_Right_And_Down_In_School)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Face_Right)
    applymovement(Rival, Movement_Rival_Goes_Halfway_Down_In_School_Player_Face_Up)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_Face_Down)
    applymovement(Rival, Movement_Rival_Fully_Leaves_Player_Face_Up)
    waitmovement(0)
    playse(SE_EXIT)
    removeobject(Rival)
    setflag(FLAG_HIDE_BARRY_IN_SCHOOL)
    release()
    end()
}

script Jubilife_City_School_EventScripts_Youngster_Near_Book
{

    msgbox(Jubilife_City_School_EventScripts_Youngster_Near_Book_Text, MSGBOX_NPC)
    end()
}

script Jubilife_City_School_EventScripts_Lady_Near_Book
{

    msgbox(Jubilife_City_School_EventScripts_Lady_Near_Book_Text, MSGBOX_NPC)
    end()
}


script Jubilife_City_School_EventScripts_Lady_Near_Board
{

    msgbox(Jubilife_City_School_EventScripts_Lady_Near_Board_Text, MSGBOX_NPC)
    end()
}

script Jubilife_City_School_EventScripts_Youngster_Near_Pc
{

    msgbox(Jubilife_City_School_EventScripts_Youngster_Near_Pc_Text, MSGBOX_NPC)
    end()
}

script Jubilife_City_School_EventScripts_School_Boy
{
    if (flag(FLAG_JUBILIFE_SCHOOL_DEFEATED_BOY))
    {
        if (flag(FLAG_RECEIVED_POTION_JUBILIFE_SCHOOL))
        {
            msgbox(Jubilife_City_School_Boy_Explain_Potion, MSGBOX_NPC)
        }
        else
        {
            if (flag(FLAG_JUBILIFE_SCHOOL_DEFEATED_GIRL))
            {
                msgbox(Jubilife_City_School_Boy_You_Are_Really_Good, MSGBOX_SIGN)
                giveitem(ITEM_POTION)
                setflag(FLAG_RECEIVED_POTION_JUBILIFE_SCHOOL)
                msgbox(Jubilife_City_School_Boy_Explain_Potion, MSGBOX_SIGN)
            }
            else
            {
                msgbox(Jubilife_City_School_Boy_Not_Give_Potion_Text, MSGBOX_SIGN)
            }
        }
        end()
    }
    else
    {
        msgbox(Jubilife_City_School_Boy_Doing_Is_Best_Way_To_Learn, MSGBOX_YESNO)
        goto_if_eq(VAR_RESULT, YES, Jubilife_City_School_EventScripts_School_Boy_Battle_Accepted)
        goto_if_eq(VAR_RESULT, NO, Jubilife_City_School_EventScripts_School_Boy_Battle_Denied)
    }
}

script Jubilife_City_School_EventScripts_School_Boy_Battle_Accepted
{
    msgbox(Jubilife_City_School_Boy_Before_Battle, MSGBOX_SIGN)
    trainerbattle_no_intro(TRAINER_HARRISON, Jubilife_City_School_Boy_Defeated)
    setflag(FLAG_JUBILIFE_SCHOOL_DEFEATED_BOY)
    if (flag(FLAG_JUBILIFE_SCHOOL_DEFEATED_GIRL))
    {
        msgbox(Jubilife_City_School_Boy_You_Are_Really_Good, MSGBOX_SIGN)
        giveitem(ITEM_POTION)
        setflag(FLAG_RECEIVED_POTION_JUBILIFE_SCHOOL)
    }
    else
    {
        msgbox(Jubilife_City_School_Boy_Not_Give_Potion_Text, MSGBOX_SIGN)
    }
    end()
}

script Jubilife_City_School_EventScripts_School_Boy_Battle_Denied
{
    msgbox(Jubilife_City_School_EventScripts_School_Boy_Battle_Denied_Text, MSGBOX_SIGN)
    end()
}

script Jubilife_City_School_EventScripts_School_Girl
{
    if (flag(FLAG_JUBILIFE_SCHOOL_DEFEATED_GIRL))
    {
        if (flag(FLAG_RECEIVED_POTION_JUBILIFE_SCHOOL))
        {
            msgbox(Jubilife_City_School_Girl_Could_Have_Done_Better, MSGBOX_NPC)
        }
        else
        {
            if (flag(FLAG_JUBILIFE_SCHOOL_DEFEATED_BOY))
            {
                msgbox(Jubilife_City_School_Girl_Talk_To_Boy, MSGBOX_SIGN)
            }
            else
            {
                msgbox(Jubilife_City_School_Girl_Could_Have_Done_Better, MSGBOX_NPC)
            }
        }
        end()
    }
    else
    {
        msgbox(Jubilife_City_School_Girl_I_Just_Became_Friends, MSGBOX_YESNO)
        goto_if_eq(VAR_RESULT, YES, Jubilife_City_School_EventScripts_School_Girl_Battle_Accepted)
        goto_if_eq(VAR_RESULT, NO, Jubilife_City_School_EventScripts_School_Girl_Battle_Denied)
    }
}

script Jubilife_City_School_EventScripts_School_Girl_Battle_Accepted
{
    msgbox(Jubilife_City_School_Girl_Before_Battle, MSGBOX_SIGN)
    trainerbattle_no_intro(TRAINER_CHRISTINE, Jubilife_City_School_Girl_Defeated)
    setflag(FLAG_JUBILIFE_SCHOOL_DEFEATED_GIRL)
    if (flag(FLAG_JUBILIFE_SCHOOL_DEFEATED_BOY))
    {
        msgbox(Jubilife_City_School_Girl_Talk_To_Boy, MSGBOX_SIGN)
    }
    else
    {
        msgbox(Jubilife_City_School_Girl_Could_Have_Done_Better, MSGBOX_SIGN)
    }
    end()
}

script Jubilife_City_School_EventScripts_School_Girl_Battle_Denied
{
    msgbox(Jubilife_City_School_EventScripts_School_Girl_Battle_Denied_Text, MSGBOX_SIGN)
    end()
}

script Jubilife_City_School_EventScripts_Blackboard
{
    lockall()
    msgbox(Jubilife_City_School_Text_Blackboard_Lists_Status_Changes, MSGBOX_SIGN)
    goto(Jubilife_City_School_EventScripts_Choose_Blackboard_Topic)
}

script Jubilife_City_School_EventScripts_Choose_Blackboard_Topic
{
    message(Jubilife_City_School_Text__Read_Which_Topic)
    waitmessage()
    multichoicegrid(8, 1, MULTI_STATUS_INFO, 2, FALSE)
    switch (var(VAR_RESULT))
    {
        case 0: goto(Jubilife_City_School_EventScripts_Poison)
        case 1: goto(Jubilife_City_School_EventScripts_Burn)
        case 2: goto(Jubilife_City_School_EventScripts_Paralysis)
        case 3: goto(Jubilife_City_School_EventScripts_Freeze)
        case 4: goto(Jubilife_City_School_EventScripts_Sleep)
        case 5: goto(Jubilife_City_School_EventScripts_ExitTopicSelect)
        case MULTI_B_PRESSED: goto(Jubilife_City_School_EventScripts_ExitTopicSelect)
    }
}

script Jubilife_City_School_EventScripts_Poison
{
    msgbox(Jubilife_City_School_EventScripts_Poison_Text, MSGBOX_SIGN)
    goto(Jubilife_City_School_EventScripts_Choose_Blackboard_Topic)
}

script Jubilife_City_School_EventScripts_Paralysis
{
    msgbox(Jubilife_City_School_EventScripts_Paralysis_Text, MSGBOX_SIGN)
    goto(Jubilife_City_School_EventScripts_Choose_Blackboard_Topic)
}

script Jubilife_City_School_EventScripts_Sleep
{
    msgbox(Jubilife_City_School_EventScripts_Sleep_Text, MSGBOX_SIGN)
    goto(Jubilife_City_School_EventScripts_Choose_Blackboard_Topic)
}

script Jubilife_City_School_EventScripts_Burn
{
    msgbox(Jubilife_City_School_EventScripts_Burn_Text, MSGBOX_SIGN)
    goto(Jubilife_City_School_EventScripts_Choose_Blackboard_Topic)
}

script Jubilife_City_School_EventScripts_Freeze
{
    msgbox(Jubilife_City_School_EventScripts_Freeze_Text, MSGBOX_SIGN)
    goto(Jubilife_City_School_EventScripts_Choose_Blackboard_Topic)
}

script Jubilife_City_School_EventScripts_ExitTopicSelect
{
    releaseall()
    end()
}

script Jubilife_City_School_EventScripts_Book
{
    msgbox(Jubilife_City_School_EventScripts_Book_Text, MSGBOX_SIGN)
    end()
}

script Jubilife_City_School_EventScripts_Big_Book_Shelf
{
    msgbox(Jubilife_City_School_EventScripts_Big_Book_Shelf_Text, MSGBOX_SIGN)
    end()
}

script Jubilife_City_School_EventScripts_Small_Book_Shelf
{
    msgbox(Jubilife_City_School_EventScripts_Small_Book_Shelf_Text, MSGBOX_SIGN)
    end()
}

text Jubilife_City_School_Text_Barry_What_Brings_You_Here
{
    "{B_RIVAL_NAME}: Hey, {PLAYER}!\n"
    "Did you come to study, too?\p"
    "I went ahead and memorized everything\n"
    "that was up on the blackboard.\p"
    "After all, it’s the Trainer’s job to\n"
    "avoid having their precious Pokémon\l"
    "hurt in battle, right?\p"
    "So, {PLAYER}, what brings you?\n"
    "Huh?\l"
    "You’ve got something for me?"
}

text Jubilife_City_School_Text_Barry_Player_Gives_Parcel
{
    "{PLAYER} delivered the Parcel."
}


text Jubilife_City_School_Text_Barry_Gives_You_Town_Map
{
    "{B_RIVAL_NAME}: So, what is this...?\p"
    "Score!\n"
    "It’s a Town Map!\p"
    "Huh?!\n"
    "Why are there two in here?\p"
    "I like it a lot, but I don’t need two.\n"
    "Here, {PLAYER}, you take one!"
}
text Jubilife_City_School_Text_Barry_Where_To_Go_Next
{
    "{B_RIVAL_NAME}: Hmm...\p"
    "Well, according to the Town Map,\n"
    "I guess Oreburgh City is where\l"
    "I should be going next.\p"
    "There’s a Gym, so it’d be perfect for\n"
    "raising the Pokémon I just caught.\p"
    "Well, I’m on the road to becoming\n"
    "the greatest Trainer of all time!\p"
    "See you around!"
}

text Jubilife_City_School_EventScripts_Youngster_Near_Book_Text
{
    "I wrote an essay in my notebook on \n"
    "what makes a true Pokémon Trainer."
}

text Jubilife_City_School_EventScripts_Lady_Near_Book_Text
{
    "Pokémon are smart enough to use\n"
    "items they’re holding.\p"
    "But they won’t know what to do with\n"
    "man-made items like Potions and\l"
    "Antidotes."
}

text Jubilife_City_School_EventScripts_Lady_Near_Board_Text
{
    "It’s perfectly fine for Trainers and\n"
    "Pokémon to develop at their own pace.\p"
    "If there is anything you’re not familiar\n"
    "with, you can look it up here."
}

text Jubilife_City_School_EventScripts_Youngster_Near_Pc_Text
{
    "We learned about Pokémon types\n"
    "today. Umm...\p"
    "Grass is weak to Fire,\p"
    "Fire is weak to Water,\p"
    "and Water is weak to Grass!"
}

text Jubilife_City_School_Boy_Doing_Is_Best_Way_To_Learn
{
    "Doing it is the best way to learn!\n"
    "Can we get a battle with you?"
}

text Jubilife_City_School_Boy_Defeated
{
    "Ugh...\n"
    "I didn’t use items effectively."
}

text Jubilife_City_School_EventScripts_School_Boy_Battle_Denied_Text
{
    "Aww! But battling is the best way\n"
    "to learn about Pokémon!"
}

text Jubilife_City_School_Boy_You_Are_Really_Good
{
    "Whew! You’re really good!\p"
    "We’ve been studying every day, but\n"
    "we were no match for you at all!\p"
    "You’d be able to use this Potion\n"
    "properly. Here you go!"
}

text Jubilife_City_School_Boy_Not_Give_Potion_Text
{
    "It’s hard to win if you don’t have\n"
    "some Pokémon to choose from."
}

text Jubilife_City_School_Boy_Explain_Potion
{
    "When a Potion is used, it restores the\n"
    "HP, the health, of a Pokémon.\p"
    "Being a good Trainer isn’t only about\n"
    "ordering your Pokémon around.\p"
    "A superior Trainer knows how to use\n"
    "items to the full advantage!"
}

text Jubilife_City_School_Girl_Could_Have_Done_Better
{
    "I wonder if we could have done better\n"
    "gainst you?\p"
    "Like if I used Potions right when we\n"
    "needed to."
}

text Jubilife_City_School_Girl_Talk_To_Boy
{
    "We were actually confident that we\n"
    "would do pretty good.\p"
    "But I’m completely impressed by how\n"
    "tough you are.\p"
    "If you’d like, talk to my friend and\n"
    "get a nice item from him."
}

text Jubilife_City_School_Girl_I_Just_Became_Friends
{
    "I just became friends with my\n"
    "Pokémon.\p"
    "I’m not sure if we can do this\n"
    "properly, but may we battle?"
}

text Jubilife_City_School_EventScripts_School_Girl_Battle_Denied_Text
{
    "Boo!\n"
    "Where’s your sense of adventure?\p"
    "Don’t be shy about talking to people\n"
    "and getting into battles!"
}

text Jubilife_City_School_Girl_Before_Battle
{
    "I need to try the things I learned\n"
    "before I forget them!"
}

text Jubilife_City_School_Boy_Before_Battle
{
    "Go!\n"
    "My Pokémon, go!"
}

text Jubilife_City_School_Girl_Defeated
{
    "I like to study a lot...\n"
    "I thought I could win..."
}

text Jubilife_City_School_Text_Blackboard_Lists_Status_Changes
{
    "The blackboard lists status changes\n"
    "that may affect PoKémon in battle."
}

text Jubilife_City_School_Text__Read_Which_Topic
{
	"Which would you like to read about?"
}

text Jubilife_City_School_EventScripts_Poison_Text
{
	"If a PoKémon is poisoned, it will\n"
	"steadily lose HP.\p"
	"The effects of poison remain after\n"
	"a battle.\p"
	"Heal a poisoning using an Antidote."
}

text Jubilife_City_School_EventScripts_Paralysis_Text
{
	"If a PoKémon becomes paralyzed,\n"
	"its Speed stat drops.\p"
	"In addition, it may not be able to\n"
	"move while it is in battle.\p"
	"Paralysis remains after battle.\n"
	"Cure it using a Paralyze Heal."
}

text Jubilife_City_School_EventScripts_Sleep_Text
{
	"If a PoKémon falls asleep, it will be\n"
	"unable to attack.\p"
	"A PoKémon may wake up on its own,\n"
	"but if a battle ends while it is\l"
	"sleeping, it will stay asleep.\p"
	"Wake it up using an Awakening."
}

text Jubilife_City_School_EventScripts_Burn_Text
{
	"A burn reduces the Attack power stat and\n"
	"steadily reduces the victim's HP.\p"
	"A burn lingers after battle.\n"
	"Cure a burn using a BURN HEAL."
}

text Jubilife_City_School_EventScripts_Freeze_Text
{
	"If a PoKémon is frozen, it becomes\n"
	"completely helpless.\p"
	"The PoKémon may thaw out on it's own\n"
	"but if a battle ends while it is\l"
	"frozen, it may stay frozen\n"
	"Thaw it out using an Ice Heal."
}

text Jubilife_City_School_EventScripts_Book_Text
{
    "It’s a notebook filled with writing.\n"
    "Let’s see...\p"
    "Pokémon are to be caught using\n"
    "Poké Balls.\p"
    "Up to six Pokémon can accompany\n"
    "a Trainer.\p"
    "A Trainer is someone who catches\n"
    "Pokémon, raises them, and battles\l"
    "with them.\p"
    "A Trainer’s mission is to defeat the\n"
    "strong Trainers who await challengers\l"
    "in Pokémon Gyms."
}

text Jubilife_City_School_EventScripts_Big_Book_Shelf_Text
{
    "It’s chock-full of Pokémon books and\n"
    "photo collections."
}

text Jubilife_City_School_EventScripts_Small_Book_Shelf_Text
{
    "It’s lined with Pokémon picture books."
}

movement Movement_Rival_Walks_Right_And_Down_In_School
{
    walk_fast_right
    walk_fast_down
}

movement Movement_Rival_Goes_Halfway_Down_In_School_Player_Face_Right_Or_Left
{
    walk_fast_down * 3
}

movement Movement_Rival_Goes_Halfway_Down_In_School_Player_Face_Up
{
    walk_fast_down * 2
}

movement Movement_Rival_Fully_Leaves_Player_Face_Right_Or_Left
{
    walk_fast_right
    walk_fast_down * 5
}


movement Movement_Rival_Fully_Leaves_Player_Face_Up
{
    walk_fast_down * 5
}
